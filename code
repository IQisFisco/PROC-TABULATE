/*Loading Data*/
DATA WORK.Energy;
   LENGTH state $2;
   INPUT region division state $ type expenditures;
   DATALINES;
1 1 ME 1 708
1 1 ME 2 379
1 1 NH 1 597
1 1 NH 2 301
1 1 VT 1 353
1 1 VT 2 188
1 1 MA 1 3264
1 1 MA 2 2498
1 1 RI 1 531
1 1 RI 2 358
1 1 CT 1 2024
1 1 CT 2 1405
1 2 NY 1 8786
1 2 NY 2 7825
1 2 NJ 1 4115
1 2 NJ 2 3558
1 2 PA 1 6478
1 2 PA 2 3695
4 3 MT 1 322
4 3 MT 2 232
4 3 ID 1 392
4 3 ID 2 298
4 3 WY 1 194
4 3 WY 2 184
4 3 CO 1 1215
4 3 CO 2 1173
4 3 NM 1 545
4 3 NM 2 578
4 3 AZ 1 1694
4 3 AZ 2 1448
4 3 UT 1 621
4 3 UT 2 438
4 3 NV 1 493
4 3 NV 2 378
4 4 WA 1 1680
4 4 WA 2 1122
4 4 OR 1 1014
4 4 OR 2 756
4 4 CA 1 10643
4 4 CA 2 10114
4 4 AK 1 349
4 4 AK 2 329
4 4 HI 1 273
4 4 HI 2 298
;

/*Creating formats for easier understanding of the data*/
PROC FORMAT;
   VALUE regfmt  1 = 'Northeast'
                 2 = 'South'
                 3 = 'Midwest'
                 4 = 'West';
   VALUE divfmt  1 = 'New England'
                 2 = 'Middle Atlantic'
                 3 = 'Mountain'
                 4 = 'Pacific';
   VALUE typefmt 1 = 'Residential Customers'
                 2 = 'Business Customers';
RUN;

/*PROC TABULATE First Output*/
TITLE "First attempt at creating a report output";
PROC TABULATE DATA = WORK.Energy;
	CLASS region division type;
	VAR expenditures;
	TABLE region * division, 
			type * expenditures;
RUN;
TITLE;

/*PROC TABULATE Second Output*/
TITLE "Second attempt at creating a report output";
PROC TABULATE DATA = WORK.Energy;
	CLASS region division type;
	VAR expenditures;
	TABLE region * division, 
			type * expenditures;
	FORMAT region regfmt.
			division divfmt.
			type typefmt.; 
RUN;
TITLE;

TITLE "Third attempt at creating a report output";
PROC TABULATE DATA = WORK.Energy;
	CLASS region division type;
	VAR expenditures;
	TABLE region = "Region" * division = "Division", 
			type = "Customer Base" * expenditures = " " * sum = " ";
	FORMAT region regfmt.
			division divfmt.
			type typefmt.; 
RUN;
TITLE;

TITLE "Fourth attempt at creating a report output";
PROC TABULATE DATA = WORK.Energy FORMAT = dollar12.;
	CLASS region division type;
	VAR expenditures;
	TABLE region = "Region" * division = "Division"
			ALL = "Total Expenditures by Regions"
			, 
			type = "Customer Base" * expenditures = " " * SUM = " "
			ALL = "Total Expenditures by Customer type" * expenditures = " " * SUM = " ";
	FORMAT region regfmt.
			division divfmt.
			type typefmt.; 
RUN;
TITLE;

TITLE "Energy Expenditures";
TITLE2 "(millions of dollars)";
PROC TABULATE DATA = WORK.Energy FORMAT = dollar12.;
	CLASS region division type 
		/ STYLE = [ textalign = center ];
	VAR expenditures 
		/ STYLE = [ fontsize = 3 ];
	TABLE region = "Region" * division = "Division"
			ALL = "Total Expenditures by Regions" * [ STYLE = [ backgroundcolor = lightgrey ] ]
			, 
			type = "Customer Base" * expenditures = " " * SUM = " "
			ALL = "Total Expenditures by Customer type" * expenditures = " " * SUM = " " * [ STYLE = [ backgroundcolor = lightgrey ] ] 
			/
			BOX = [ LABEL = "Region by Division by Customer Type" ];
	FORMAT region regfmt.
			division divfmt.
			type typefmt.; 
RUN;
TITLE;
